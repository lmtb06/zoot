package zoot.analyse ;

import java.util.*;
import zoot.analyse.* ;
import zoot.arbre.* ;
import zoot.arbre.expressions.* ;
import zoot.arbre.instructions.* ;
import zoot.code_generation.* ;
import zoot.exceptions.*;
import zoot.tds.*;
import zoot.tds.entrees.*;
import zoot.tds.symboles.*;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non terminée") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    VARIABLES, FONCTIONS, DEBUT, FIN, POINTVIRGULE, ECRIRE, EGAL, PARENTHESEOUVERTE,
PARENTHESEFERMEE, RETOURNE ;
terminal String    TYPE, CSTENTIERE, CSTBOOL, IDF ;

/* Non terminals used in the grammar section.  */
non terminal Programme PROG;
non terminal BlocDeFonctions LFONC;
non terminal BlocDInstructions LINST, LINSTF;
non terminal Fonction FONC;
non terminal Instruction INST, INSTF, RETOURNEI ;
non terminal Expression EXP ;
non terminal LDECL, DECL, PARAMETRES, PAR_EFF ;

/* ----------------------------Grammar Section-------------------- */

start with PROG ;

PROG    ::=     VARIABLES:v LDECL:ld FONCTIONS LFONC:lf DEBUT  LINST:li  FIN
                {:
                    // Je n'ai pas mis de entreeBloc et sortieBloc pour le main pour simplifier, c'est géré dans
                    // l'initialisation de la TDS
                    Programme main = new Programme(lf, li, vleft + 1);
                    TDS.getInstance().allerEtatSuivant();
                    RESULT = main ;
                :}
                ;

LFONC   ::=     LFONC:bf FONC:f
                {:
                    bf.ajouter(f);
                    RESULT = bf;
                 :}
        |
                FONC:f
                {:
                    BlocDeFonctions bf = new BlocDeFonctions(fleft + 1) ;
                    bf.ajouter(f) ;
                    RESULT = bf ;
                :}
                ;

PARAMETRES ::=  PARENTHESEOUVERTE PARENTHESEFERMEE
                {:

                :}
                ;

FONC    ::=     TYPE:t IDF:i PARAMETRES DEBUT LINSTF:li  FIN
                {:

                    Type typeSymbole = Type.ENTIER;
                    switch (t) {
                        case "entier" :
                            typeSymbole = Type.ENTIER;
                            break;
                        case "booleen" :
                            typeSymbole = Type.BOOLEEN;
                            break;
                    }

                    EntreeFonction entreeFonction = new EntreeFonction(i, new ArrayList<Type>(0));



                    TDS.getInstance().entreeBloc();
                    SymboleFonction symboleFonction = new SymboleFonction(typeSymbole, TDS.getInstance().getEspaceDeNomCourant());
                    // Ajout des déclarations
                    TDS.getInstance().sortieBloc();

                    symboleFonction.setEtiquette(entreeFonction.getIdentifiant() + TDS.getInstance().getNiveauImbricationCourant());
                    TDS.getInstance().ajouter(entreeFonction, symboleFonction);

                    Fonction f = new Fonction(entreeFonction, symboleFonction, tleft + 1);
                    f.ajouter(li);

                    RESULT = f;
                :}
                ;

LINSTF  ::=     LINSTF:bi  INSTF:i
                {:
                    bi.ajouter(i) ;
                    RESULT = bi ;
                :}
        |
                INSTF:i
                {:
                    BlocDInstructions bi = new BlocDInstructions(ileft + 1) ;
                    bi.ajouter(i) ;
                    RESULT = bi ;
                :}
                 ;

INSTF   ::=     INST:i
                {:
                    RESULT = i;
                :}
        |
                RETOURNEI:i
                {:
                    RESULT = i;
                :}
                ;

RETOURNEI   ::= RETOURNE EXP:e POINTVIRGULE
                {:
                    RESULT = new Retourne(e, eleft + 1) ;
                :}
                ;

LDECL   ::=     DECL LDECL
        |
                DECL;

DECL    ::=     TYPE:t IDF:i POINTVIRGULE
                {:
                    Type typeSymbole = Type.ENTIER;
                    switch (t) {
                        case "entier" :
                            typeSymbole = Type.ENTIER;
                            break;
                        case "booleen" :
                            typeSymbole = Type.BOOLEEN;
                            break;
                    }


                    SymboleVariable s = new SymboleVariable(typeSymbole, TDS.getInstance().getEspaceDeNomCourant());
                    try{
                        TDS.getInstance().ajouter(new EntreeVariable(i), s);
                    } catch (DoubleDeclarationException e) {
                        GestionnaireExceptionsSemantiques.getInstance()
                                            .ajouter(new LigneDecorator(ileft + 1, e));
                    }
                    s.setDeplacement(-TDS.getInstance().getTailleZoneVariables());
                    TDS.getInstance().augmenterTailleZoneVariables(4);
                :};

LINST   ::=     LINST:bi  INST:i
                {: bi.ajouter(i) ;
                   RESULT = bi ; :}
        |
                INST:i
                {: BlocDInstructions bi = new BlocDInstructions(ileft + 1) ;
                   bi.ajouter(i) ;
                   RESULT = bi ; :}
                ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
        |
                IDF:i EGAL EXP:e POINTVIRGULE
                {:
                    Variable v = new Variable(new EntreeVariable(i), ileft + 1);
                    RESULT = new Affectation(v, e, ileft + 1) ;
                :}
                ;
    
EXP    ::=     CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
        |
               CSTBOOL:c
                {:
                    RESULT = new ConstanteBooleene(c, cleft + 1) ;
                :}
        |
               IDF:i
                {:
                    RESULT = new Variable(new EntreeVariable(i), ileft + 1) ;
                :}
        |
               IDF:i PAR_EFF:p
               {:
                    EntreeFonction entreeFonction = new EntreeFonction(i, new ArrayList<Type>(0));
                    RESULT = new AppelFonction(entreeFonction, ileft + 1);
               :}
                ;

PAR_EFF ::=  PARENTHESEOUVERTE PARENTHESEFERMEE
                ;
