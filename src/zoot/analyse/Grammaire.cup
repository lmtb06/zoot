package zoot.analyse ;

import java.util.*;
import zoot.analyse.* ;
import zoot.arbre.* ;
import zoot.arbre.expressions.* ;
import zoot.arbre.instructions.* ;
import zoot.code_generation.* ;
import zoot.exceptions.*;
import zoot.tds.*;
import zoot.tds.entrees.*;
import zoot.tds.symboles.*;

import java_cup.runtime.*;

action code {:
             :} ;

parser code {:

    public void report_error(String message, Object info) {

        HashMap<Integer, String> lesTerminaux = new HashMap<>() ;
    
        lesTerminaux.put(new Integer(CodesLexicaux.DEBUT), "debut") ;
        lesTerminaux.put(new Integer(CodesLexicaux.FIN), "fin") ;
        lesTerminaux.put(new Integer(CodesLexicaux.POINTVIRGULE), ";") ;

        StringBuffer m = new StringBuffer() ;

        if (info instanceof java_cup.runtime.Symbol) {
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

            if (s.left >= 0) {                
                m.append("\tligne : " + (s.left + 1)) ;
                if (s.right >= 0)                    
                    m.append(" colonne : " + (s.right+1)) ;
            }
            
            if (s.value != null) {
                lesTerminaux.put(CodesLexicaux.CSTENTIERE, "" + s.value) ;
            }

            if (lesTerminaux.containsKey(new Integer(s.sym))) {
                m.append(" dernier token lu : " + lesTerminaux.get(new Integer(s.sym))) ;
            }
            else {
                m.append(" expression non terminée") ;
            }

        }
        throw new AnalyseSyntaxiqueException("" + m) ;
    }

    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
    }
:};
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  */

terminal Symbol    VARIABLES, FONCTIONS, DEBUT, FIN, POINTVIRGULE, ECRIRE, EGAL, PARENTHESEOUVERTE,
PARENTHESEFERMEE, RETOURNE ;
terminal String    TYPE, CSTENTIERE, CSTBOOL, IDF ;

/* Non terminals used in the grammar section.  */

non terminal ArbreAbstrait PROG, LINST, LFONC, FONC, LINSTF;
non terminal Instruction INST, INSTF, RETOURNEI ;
non terminal Expression EXP ;
non terminal LDECL, DECL, PARAMETRES ;

/* ----------------------------Grammar Section-------------------- */

start with PROG ;

PROG    ::=     VARIABLES LDECL:ld FONCTIONS LFONC:lf DEBUT  LINST:li  FIN
                {:
                    // TODO Ajout des fonctions une fois les bonnes classes implémentées
                    RESULT = li ;
                :}
                ;

LFONC   ::=     LFONC:lf FONC:f
                {: /* Ajout de la fonction f à lf */
                    RESULT = f; // TODO mettre le bon retour
                 :}
        |
                FONC:f
                {: /* Création de lf */
                    RESULT = f; // TODO mettre le bon retour
                :}
                ;

FONC    ::=     TYPE:t IDF:i PARAMETRES DEBUT LINSTF:li  FIN
                {:
                    RESULT = li ; // TODO Mettre le bon retour
                :}
                ;



PARAMETRES ::=  PARENTHESEOUVERTE PARENTHESEFERMEE
                ;

LINSTF  ::=     LINSTF:li  INSTF:i
                {:
                    ((BlocDInstructions)li).ajouter(i) ;
                    RESULT = li ;
                :}
        |
                INSTF:i
                {:
                    BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                    b.ajouter(i) ;
                    RESULT = b ;
                :}
                 ;

INSTF   ::=     INST
        |
                RETOURNEI
                ;

RETOURNEI   ::= RETOURNE EXP:e POINTVIRGULE
                {:
                    RESULT = new Ecrire(e, eleft + 1) ; // TODO Mettre la bonne classe
                :}
                ;

LDECL   ::=     DECL LDECL
        |
                DECL;

DECL    ::=     TYPE:t IDF:i POINTVIRGULE
                {:
                    Type typeSymbole = Type.ENTIER;
                    switch (t) {
                        case "entier" :
                            typeSymbole = Type.ENTIER;
                            break;
                        case "booleen" :
                            typeSymbole = Type.BOOLEEN;
                            break;
                    }


                    SymboleVariable s = new SymboleVariable(typeSymbole);
                    try{
                        TDS.getInstance().ajouter(new EntreeVariable(i), s);
                    } catch (DoubleDeclarationException e) {
                        GestionnaireExceptionsSemantiques.getInstance()
                                            .ajouter(new LigneDecorator(ileft + 1, e));
                    }
                    s.setDeplacement(TDS.getInstance().getTailleZoneVariables());
                    TDS.getInstance().augmenterTailleZoneVariables(4);
                :};

LINST   ::=     LINST:li  INST:i
                {: ((BlocDInstructions)li).ajouter(i) ;
                   RESULT = li ; :}
        |
                INST:i
                {: BlocDInstructions b = new BlocDInstructions(ileft + 1) ;
                   b.ajouter(i) ;
                   RESULT = b ; :}
                ;

INST    ::=     ECRIRE EXP:e POINTVIRGULE
                {: RESULT = new Ecrire(e, eleft + 1) ; :}
        |
                IDF:i EGAL EXP:e POINTVIRGULE
                {:
                    Variable v = new Variable(new EntreeVariable(i), ileft + 1);
                    RESULT = new Affectation(v, e, ileft + 1) ;
                :}
                ;
    
EXP    ::=     CSTENTIERE:c
                {: RESULT = new ConstanteEntiere(c, cleft + 1) ; :}
        |
               CSTBOOL:c
                {:
                    RESULT = new ConstanteBooleene(c, cleft + 1) ;
                :}
        |
               IDF:i
                {:
                    RESULT = new Variable(new EntreeVariable(i), ileft + 1) ;
                :}
                ;
